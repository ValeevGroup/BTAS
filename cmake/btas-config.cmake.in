# - CMAKE Config file for the BTAS package
# This will define the following CMake cache variables
#
#    BTAS_FOUND           - true if BTAS library were found
#    BTAS_VERSION         - the BTAS version
#    BTAS_EXT_VERSION     - the BTAS version including the (optional) buildid, such as beta.3
#
# and the following imported targets
#
#     BTAS::BTAS          - the BTAS library
#

# Set package version
set(BTAS_VERSION "@BTAS_VERSION@")
set(BTAS_EXT_VERSION "@BTAS_EXT_VERSION@")

@PACKAGE_INIT@

@Boost_CONFIG_FILE_CONTENTS@

# find linalgpp dependencies
if(NOT TARGET blaspp)
  include( CMakeFindDependencyMacro )
  get_filename_component(blaspp_DIR "@blaspp_CONFIG@" DIRECTORY)
  if (NOT DEFINED BLAS_LIBRARIES AND NOT DEFINED blaspp_defs_)
    if (NOT "@BLAS_LIBRARIES@" STREQUAL "")
      set(BLAS_LIBRARIES "@BLAS_LIBRARIES@" CACHE STRING "BLAS_LIBRARIES used during BTAS configuration")
    endif()
    if (NOT "@blaspp_defs_@" STREQUAL "")
      set(blaspp_defs_ "@blaspp_defs_@" CACHE STRING "blaspp_defs_ used during BTAS configuration")
    endif()
  endif()
  find_dependency( blaspp CONFIG REQUIRED HINTS "${blaspp_DIR}" )

  # if need Threads::Threads, load it
  get_target_property(blaspp_LINK_LIBRARIES blaspp INTERFACE_LINK_LIBRARIES)
  if (Threads::Threads IN_LIST blaspp_LINK_LIBRARIES AND NOT TARGET Threads::Threads)
    find_dependency(Threads)
    # Threads::Threads by default is not GLOBAL, so to allow users of LINALG_LIBRARIES to safely use it we need to make it global
    # more discussion here: https://gitlab.kitware.com/cmake/cmake/-/issues/17256
    set_target_properties(Threads::Threads PROPERTIES IMPORTED_GLOBAL TRUE)
  endif()

endif()
if(NOT TARGET lapackpp)
  include( CMakeFindDependencyMacro )
  get_filename_component(lapackpp_DIR "@lapackpp_CONFIG@" DIRECTORY)
  if (NOT DEFINED LAPACK_LIBRARIES AND NOT DEFINED lapackpp_defs_)
    if (NOT "@LAPACK_LIBRARIES@" STREQUAL "")
      set(LAPACK_LIBRARIES "@LAPACK_LIBRARIES@" CACHE STRING "LAPACK_LIBRARIES used during BTAS configuration")
    endif()
    if (NOT "@lapackpp_defs_@" STREQUAL "")
      set(lapackpp_defs_ "@lapackpp_defs_@" CACHE STRING "lapackpp_defs_ used during BTAS configuration")
    endif()
  endif()
  find_dependency( lapackpp CONFIG REQUIRED HINTS "${lapackpp_DIR}" )
endif()

# Include library IMPORT targets
if(NOT TARGET blaspp_headers)
  include("${blaspp_DIR}/blaspp_headers-targets.cmake")
  if(NOT TARGET blaspp_headers)
    message(FATAL_ERROR "expected blaspp_headers among imported BTAS targets")
  endif()
endif()
if(NOT TARGET BTAS::BTAS)
  include("${CMAKE_CURRENT_LIST_DIR}/btas-targets.cmake")
  if(NOT TARGET BTAS::BTAS)
    message(FATAL_ERROR "expected BTAS::BTAS among imported BTAS targets")
  endif()
endif()

set(BTAS_FOUND TRUE)
